// Coleira Transmissora
#include <Arduino.h>
#include <TinyGPS++.h>
#include <SPI.h>
#include <LoRa.h>
#include <WiFi.h>

//LoRa Pinos
#define LORA_CS_PIN   26
#define LORA_RST_PIN  27
#define LORA_IRQ_PIN  33

//GPS Pinos
#define RX_PIN 16
#define TX_PIN 17

TinyGPSPlus gps;
HardwareSerial gpsSerial(2);

int marcador = 0;
int counter = 0;
unsigned long lastSendTime = 0; 
const unsigned long sendInterval = 5000; // Intervalo de 5 segundos (5000 ms)

enum PacketType : uint8_t {
  UPDATE_LOCATION = 0,
  ACTIVATE_COLLAR = 1
};

#pragma pack(push, 1)
struct DataPackage {
  float latitude;
  float longitude;
  uint8_t battery;
  uint8_t mac[6];
  uint8_t type;
};
#pragma pack(pop)

DataPackage package;

void setup() {
    Serial.begin(115200);
    delay(2000);
    Serial.println("Coleira Iniciada");

  //Ativa modulo Wifi, guarda a endereço MAC, e desliga o modulo Wifi
    WiFi.mode(WIFI_STA);
    delay(100);
    WiFi.macAddress(package.mac);
    WiFi.mode(WIFI_OFF);
    Serial.print("MAC desta Coleira: ");
    for (int i = 0; i < 6; i++) {
      if (package.mac[i] < 0x10) Serial.print("0");
      Serial.print(package.mac[i], HEX);
      if (i < 5) Serial.print(":");
    }
    Serial.println();
    delay(100);

    //Inicialização do módulo LoRa
    LoRa.setPins(LORA_CS_PIN, LORA_RST_PIN, LORA_IRQ_PIN);

    if (!LoRa.begin(433E6)) {
      Serial.println("Erro ao iniciar LoRa.");
    while (1);
}
    Serial.println("LoRa iniciado com sucesso!");
    delay(1000);

    //Inicialização do módulo GPS
    Serial.println("Inicializando teste do GPS NEO-M8N (Método Recomendado)");
    gpsSerial.begin(9600, SERIAL_8N1, RX_PIN, TX_PIN);

    //Atualiza o status da coleira.
    Serial.println("Enviando pacote de ATIVAÇÃO...");
    activeCollar();
    delay(200);
}

void loop() {
    while (gpsSerial.available() > 0) {
        gps.encode(gpsSerial.read());
    }

    if (millis() - lastSendTime > sendInterval) {
        lastSendTime = millis();

        Serial.println("\nVerificando dados para envio...");
        displayInfo();
        //Faz um pacote teste para debug
        if (marcador < 1){
            marcador++;
            prepararPacoteTeste();
            enviarPacote();
            Serial.println("Pacote de Teste enviado enviado!");
        }
        //Fim do pacote de teste
        if (gps.location.isValid()) {
            prepararPacote();
            enviarPacote();
            Serial.println("Pacote enviado!");
            counter++;
        } else {
            Serial.println("Envio cancelado: Localização GPS ainda é inválida.");
        }
    }

    if (millis() > 5000 && gps.charsProcessed() < 10) {
        Serial.println("ALERTA: NENHUM DADO RECEBIDO DO GPS. VERIFIQUE A FIAÇÃO!");
        while (true);
    }
}

void activeCollar(){
  package.type = PacketType::ACTIVATE_COLLAR;
  LoRa.beginPacket();
  LoRa.write((byte*)&package, sizeof(package));
  LoRa.endPacket();
}

void prepararPacoteTeste(){
  // Coloca dados FALSOS (hardcoded) na struct
  package.latitude = -23.5505;  // Localização Fictícia (São Paulo)
  package.longitude = -46.6333; // Localização Fictícia (São Paulo)
  package.battery = 99;         // Bateria Fictícia
  package.type = PacketType::UPDATE_LOCATION;
}

void prepararPacote(){
  package.latitude = gps.location.lat();
  package.longitude = gps.location.lng();
  package.battery = 100; // Ficticio por enquanto
  package.type = PacketType::UPDATE_LOCATION;
}

void enviarPacote(){
  LoRa.beginPacket();
  // LoRa.print(dados);
  LoRa.write((byte*)&package, sizeof(package));
  LoRa.endPacket();
}

void displayInfo() {
  Serial.print("Localização: ");
  if (gps.location.isValid()) {
    Serial.print(gps.location.lat(), 6); // Latitude
    Serial.print(", ");
    Serial.print(gps.location.lng(), 6); // Longitude
  } else {
    Serial.print("INVÁLIDA");
  }

  Serial.print(" | Data: ");
  if (gps.date.isValid()) {
    Serial.print(gps.date.day());
    Serial.print("/");
    Serial.print(gps.date.month());
    Serial.print("/");
    Serial.print(gps.date.year());
  } else {
    Serial.print("INVÁLIDA");
  }

  Serial.print(" | Hora (UTC): ");
  if (gps.time.isValid()) {
    if (gps.time.hour() < 10) Serial.print(F("0"));
    Serial.print(gps.time.hour());
    Serial.print(":");
    if (gps.time.minute() < 10) Serial.print(F("0"));
    Serial.print(gps.time.minute());
    Serial.print(":");
    if (gps.time.second() < 10) Serial.print(F("0"));
    Serial.print(gps.time.second());
  } else {
    Serial.print("INVÁLIDA");
  }
  
  Serial.print(" | Satélites: ");
  if (gps.satellites.isValid()){
    Serial.print(gps.satellites.value());
  } else {
    Serial.print("INVÁLIDO");
  }

  Serial.println(); // Pula para a próxima linha
}
